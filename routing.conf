# Routing of `if' conditions
typeset -ga FAST_ROUTING__NONE=(
    # 1. assign
    '[[ $__arg == [a-zA-Z_][a-zA-Z0-9_]#(|\[[^\]]#\])(|[^\]]#\])(|[+])=* || $__arg == [0-9]##(|[+])=* || "${braces_stack[1]}" = "T" ]]'
    '[[ $__arg[1] = $histchars[1] && -n "${__arg[2]}" ]]'      # 2. history expansion
    '[[ $__arg[1] == $histchars[2] ]]'                         # 3. history expansion
    '(( (__arg_type == 3) && (this_word & 2) ))'               # 4. command separator
    '[[ $__arg[1,2] == "((" ]]'                                # 5. arithmetic
    '[[ $__arg == "()" ]]'                                     # 6. anonymous function
    '[[ $__arg == "(" ]]'                                      # 7. open subshell
    '[[ $__arg == ")" ]]'                                      # 8. close subshell
    '(( this_word & 14 ))'                                     # 9. sudo
    '[[ "$__arg" = ";;" ]] && (( this_word & BIT_case_code ))' # 10. end of `case' item
    '(( 1 ))'                                                  # 11. catch all
)

autoload -Uz hue/assign hue/history-exp hue/command-separator hue/arithmetic hue/anon \
              hue/open-subshell hue/close-subshell hue/sudo hue/end-case-item

typeset -ga FAST_DISPATCH__NONE=(
    hue/assign             # 1
    hue/history-exp        # 2
    hue/history-exp        # 3
    hue/command-separator  # 4
    hue/arithmetic         # 5
    hue/anon               # 6
    hue/open-subshell      # 7
    hue/close-subshell     # 8
    hue/sudo               # 9
    hue/end-case-item      # 10
    :
)

local __cond
integer __idx=1
for __cond in "${FAST_ROUTING__NONE[@]}"; do
    __cond="$__cond && return $__idx; "
    FAST_ROUTING__NONE[__idx]="$__cond"
    __idx+=1;
done
unset __cond __idx

functions[-fast-hue-none-choose]="${(j::)FAST_ROUTING__NONE};"
-fast-hue-none-call() { -fast-hue-none-choose; ${(0e)FAST_DISPATCH__NONE[$?]}; }

# vim:ft=zsh:et:sw=4
